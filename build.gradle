/*
 * This file is part of PROS Application Browser, a flash enabled
 * browser restricted to preconfigured servers.
 * Copyright (C) 2018 PROS, Inc.
 *
 * PROS Application Browser is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * PROS Application Browser is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with PROS Application Browser.  If not, see http://www.gnu.org/licenses/
 * You can contact PROS, Inc. with any questions at http://www.pros.com
 */

import groovy.json.JsonBuilder
import groovy.json.JsonSlurper

buildscript {
    repositories {
        maven {
            url "http://pros-maven.prosrm.com:8888/artifactory/repo"
        }
    }
    dependencies {
        classpath 'com.github.node-gradle:gradle-node-plugin:2.2.4'
    }
}
apply plugin: 'com.github.node-gradle.node'

node {
    version = '12.13.1'
    distBaseUrl = "http://pros-maven.prosrm.com:8888/artifactory/ext-releases-local/org/nodejs/node"
    download = true
    workDir = file("${buildDir}/nodejs")
}

defaultTasks 'make'

task lint(type:NpmTask) {
    description "Runs the npm lint"
    args = ['run', 'lint']
}

task make(type:NpmTask, dependsOn: ['npmInstall']) {
    description "Runs the npm make"
    args = ['run', 'make']
}

task getLicense(type:NpmTask, dependsOn: ['npmInstall']) {
    description "Runs the npm getLicense"
    args = ['run', 'getLicense']
}

task getLicenseAndMake(dependsOn: ['getLicense', 'make']) {
    description "Runs the npm getLicense and make"
}

wrapper {
   gradleVersion = '6.2.2'
   distributionUrl = "http://pros-maven.prosrm.com:8888/artifactory/plugins-releases/org/gradle/gradle/${gradleVersion}/gradle-${gradleVersion}-bin.zip"
}

def changeWinMaker = { maker ->
    def packageJsonFile = file("package.json")
    def jsonObj = new JsonSlurper().parseText(packageJsonFile.getText())
    jsonObj.config.forge.makers[0].name = maker;
    packageJsonFile.text = new JsonBuilder(jsonObj).toPrettyString()
}

task makeWix(type:NpmTask, dependsOn: ['npmInstall']) {
    doFirst() {
        changeWinMaker('@electron-forge/maker-wix')
    }
    description "Runs the npm make"
    args = ['run', 'make']
}

task makeSquirrel(type:NpmTask, dependsOn: ['npmInstall']) {
    doFirst() {
        changeWinMaker('@electron-forge/maker-squirrel')
    }
    description "Runs the npm make"
    args = ['run', 'make']
}