/*
 * This file is part of PROS Application Browser, a flash enabled
 * browser restricted to preconfigured servers.
 * Copyright (C) 2018 PROS, Inc.
 *
 * PROS Application Browser is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * PROS Application Browser is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with PROS Application Browser.  If not, see http://www.gnu.org/licenses/
 * You can contact PROS, Inc. with any questions at http://www.pros.com
 */

import groovy.json.JsonBuilder
import groovy.json.JsonSlurper
import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        maven {
            url "${repositoryURL}/artifactory/maven-onprem-dev"
            credentials {
                username = artifactory_user
                password = artifactory_password
            }
            allowInsecureProtocol = true
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
        classpath "com.thoughtworks.xstream:xstream:1.4.15"
    }
}

apply plugin: "com.jfrog.artifactory"

defaultTasks 'make'

ext {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        npmCommand = 'npm.cmd'
    } else {
        npmCommand = 'npm'
    }    
}

task npmInstall(type: Exec) {
    commandLine npmCommand, 'install'
}

task lint(type: Exec) {
    description "Runs the npm lint"
    commandLine npmCommand, 'run' ,'lint'
}

task make(type: Exec, dependsOn: ['npmInstall']) {
    description "Runs the npm make"
    commandLine npmCommand, 'run', 'make'
}

task getLicense(type: Exec, dependsOn: ['npmInstall']) {
    description "Runs the npm getLicense"
    commandLine npmCommand, 'run', 'getLicense'
}

task getLicenseAndMake(dependsOn: ['getLicense', 'make']) {
    description "Runs the npm getLicense and make"
}

wrapper {
   gradleVersion = '6.2.2'
   distributionUrl = "http://cache.devops.pros.com/services.gradle.org/distributions/gradle-${gradleVersion}-bin.zip"
}

def changeWinMaker = { maker ->
    def packageJsonFile = file("package.json")
    def jsonObj = new JsonSlurper().parseText(packageJsonFile.getText())
    jsonObj.config.forge.makers[0].name = maker;
    packageJsonFile.text = new JsonBuilder(jsonObj).toPrettyString()
}

task makeWix(type: Exec, dependsOn: ['npmInstall']) {
    doFirst() {
        changeWinMaker('@electron-forge/maker-wix')
    }
    description "Runs the npm make"
    commandLine npmCommand, 'run', 'make'
}

task makeSquirrel(type: Exec, dependsOn: ['npmInstall']) {
    doFirst() {
        changeWinMaker('@electron-forge/maker-squirrel')
    }
    description "Runs the npm make"
    commandLine npmCommand, 'run', 'make'
}
